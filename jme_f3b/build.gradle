plugins {
    id "io.github.0ffz.github-packages" version "1.2.1"
}

apply plugin: 'java'
apply plugin: 'maven-publish'

 
def v_jme3="3.6.1-stable"
def g_jme3="org.jmonkeyengine"

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def generatedProtocolPath="../format/generated/main/java"
println("Generated protocol is in "+generatedProtocolPath)


sourceSets {
    main {
        java {
            srcDirs += [generatedProtocolPath]
        }
    }
}

jar {
    exclude('f3b/**')  
}

repositories{
    maven githubPackage.invoke("riccardobl")
    mavenCentral()	
}

javadoc {
  source = sourceSets.main.allJava
  classpath = configurations.compileClasspath
}

task dumper(type: JavaExec) {
    standardInput = System.in
    main ='wf.frk.f3b.tools.F3bDumper'
    classpath file("${rootDir}/build/classes/java/main")
    classpath  sourceSets.main.compileClasspath
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

dependencies {
    implementation project(":f3b_format")
	implementation 'io.chandler:protobuf-java:3.7.1-teavm2'

    compileOnly 'wf.frk:jme3-bullet-vhacd:1.0.5'
    compileOnly "${g_jme3}:jme3-core:${v_jme3}"
    compileOnly "${g_jme3}:jme3-jbullet:${v_jme3}"
    
	testImplementation "${g_jme3}:jme3-lwjgl:${v_jme3}"
	testImplementation "${g_jme3}:jme3-desktop:${v_jme3}"
	testImplementation "${g_jme3}:jme3-jbullet:${v_jme3}"
	testImplementation 'wf.frk:jme3-bullet-vhacd:1.0.5'
	testImplementation 'junit:junit:4.12'
}

publishing {
  publications {
        mavenJava(MavenPublication) {
            from components.java

            groupId = project.group
            artifactId = project.name
            version = project.version
        }
    }

    repositories {
        maven {
            name = 'local'
            url = uri("${project.projectDir}/../dist/maven")
        }
    }
}